plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'signing'
    id 'maven'
}

archivesBaseName = 'boost-gradle-plugin'
group = 'io.openliberty.boost'
version = '0.1.1-SNAPSHOT'

def boosterVersion = '0.1.3-SNAPSHOT'

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
    maven {
      url "https://plugins.gradle.org/m2/"
    }
}

dependencies {
    compile localGroovy()
    compile 'net.wasdev.wlp.gradle.plugins:liberty-gradle-plugin:2.6.2-SNAPSHOT'
    compile group: 'commons-io', name: 'commons-io', version: '2.6'
    compile 'io.openliberty.boost:boost-common:0.1.3-SNAPSHOT'
    compile 'com.spotify:docker-client:8.11.7'

    testCompile 'junit:junit:4.12'
    testCompile("javax.ws.rs:javax.ws.rs-api:2.1")
    testCompile("com.github.docker-java:docker-java:3.1.0-rc-4")
    testCompile "commons-httpclient:commons-httpclient:3.1"
    testCompile group: 'org.glassfish', name: 'javax.json', version: '1.0.4'
    testCompile group: 'org.apache.cxf', name: 'cxf-rt-rs-client', version: '3.1.11'
    testCompile group: 'org.apache.cxf', name: 'cxf-rt-rs-extension-providers', version: '3.1.11'
    testCompile group: 'junit', name: 'junit', version: '4.+'
}

test {
    doFirst { //Copying gradle.properties with plugin version to test projects
        String runtimeGroup
        String runtimeArtifactId
        String libertyRuntime = System.getProperty('runtime')
        String runtimeVersion = System.getProperty('runtimeVersion')

        if (libertyRuntime == null || libertyRuntime.isEmpty()) {
            throw new GradleException('Tests could not be run. Please specify a Liberty runtime. Choose either wlp or ol.')
        }
        if (runtimeVersion == null || runtimeVersion.isEmpty()) {
            throw new GradleException('Tests could not be run. Please specify a Liberty runtime version.')
        }

        Properties prop = new Properties()
        OutputStream output = null

        try {
            output = new FileOutputStream("${buildDir}/gradle.properties")

            if (libertyRuntime == "ol") {
                runtimeGroup = "io.openliberty"
                runtimeArtifactId = "openliberty-runtime"
            } else {
                runtimeGroup = "com.ibm.websphere.appserver.runtime"
                runtimeArtifactId = "wlp-javaee7"
            }

            // set the properties value
            prop.setProperty("boostVersion", version)
            prop.setProperty("boosterVersion", boosterVersion)
            prop.setProperty("runtimeGroup", runtimeGroup)
            prop.setProperty("runtimeArtifactId", runtimeArtifactId)
            prop.setProperty("runtimeVersion", runtimeVersion)

            // save properties to project root folder
            prop.store(output, null)

        } catch (IOException io) {
            io.printStackTrace()
        } finally {
            if (output != null) {
                try {
                    output.close()
                } catch (IOException e) {
                    e.printStackTrace()
                }
            }
        }
    }
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

compileTestJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task groovydocJar(type: Jar) {
    classifier = 'groovydoc'
    from groovydoc
}

artifacts {
    archives groovydocJar, sourcesJar
}

if (!version.endsWith("SNAPSHOT")) {
    signing {
        sign configurations.archives
    }
}

if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
  uploadArchives {
    repositories {
      mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
          authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        pom.project {
          name 'boost-gradle-plugin'
          packaging 'jar'
          description 'Boost Gradle Plug-in.'
          url 'https://github.com/OpenLiberty/boost'

          scm {
            connection 'scm:git:git@github.com:OpenLiberty/boost.git'
            developerConnection 'scm:git:git@github.com:OpenLiberty/boost.git'
            url 'git@github.com:OpenLiberty/boost.git'
            tag 'HEAD'
          }

          licenses {
            license {
              name 'Eclipse Public License 1.0 (EPL-1.0)'
              url 'https://raw.github.com/OpenLiberty/boost/master/LICENSE'
            }
          }
        }
      }
    }
  }
}

gradlePlugin {
    plugins {
        boostPlugin {
            id = 'io.openliberty.boost'
            implementationClass = 'io.openliberty.boost.gradle.Boost'
        }
    }
}
