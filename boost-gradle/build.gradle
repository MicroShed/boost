plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven'
}

archivesBaseName = 'boost-gradle-plugin'
group = 'io.openliberty.boost'
version = '0.1-SNAPSHOT'

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
    maven {
      url "https://plugins.gradle.org/m2/"
    }
}

configurations {
    //dependencies to be included in the plugin jar
    includeLibs
}

dependencies {
    compile localGroovy()
    compile 'net.wasdev.wlp.common:ci.common:1.3.3-SNAPSHOT'
    compile 'net.wasdev.wlp.gradle.plugins:liberty-gradle-plugin:2.6-SNAPSHOT'
    compile group: 'commons-io', name: 'commons-io', version: '2.6'
    compile 'io.openliberty.boost:boost-common:0.1.2-SNAPSHOT'

    includeLibs 'gradle.plugin.com.palantir.gradle.docker:gradle-docker:0.20.1'

    configurations.compile.extendsFrom(configurations.includeLibs)

    testCompile 'junit:junit:4.12'
    testCompile("javax.ws.rs:javax.ws.rs-api:2.1")
    testCompile("com.github.docker-java:docker-java:3.1.0-rc-4")
}

jar {
    from {
        configurations.includeLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

test {
    doFirst { //Copying gradle.properties with plugin version to test projects
        Properties prop = new Properties()
        OutputStream output = null

        try {
            output = new FileOutputStream("${buildDir}/gradle.properties")

            // set the properties value
            prop.setProperty("boostVersion", version)

            // save properties to project root folder
            prop.store(output, null)

        } catch (IOException io) {
            io.printStackTrace()
        } finally {
            if (output != null) {
                try {
                    output.close()
                } catch (IOException e) {
                    e.printStackTrace()
                }
            }
        }
    }
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

compileTestJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

gradlePlugin {
    plugins {
        boostPlugin {
            id = 'io.openliberty.boost'
            implementationClass = 'io.openliberty.boost.liberty.Boost'
        }
    }
}